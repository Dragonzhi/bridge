extends Node2D
class_name Bridge

@onready var animated_sprite: AnimatedSprite2D = $AnimatedSprite2D
@onready var repair_timer: Timer = $RepairTimer
@onready var up_level_sprite: Sprite2D = $UpLevelSprite2D
@onready var hit_area: Area2D = $HitArea2D

@export var max_health: float = 100.0
@export var repair_time: float = 3.0
@export var attack_upgrade_damage: float = 5.0

var current_health: float
var grid_manager: GridManager
var grid_pos: Vector2i
var is_destroyed: bool = false
var is_attack_upgraded: bool = false
var tile_animation_name: String

signal bridge_selected(bridge: Bridge)

func _ready() -> void:
	current_health = max_health
	grid_manager = get_node("/root/Main/GridManager")
	repair_timer.wait_time = repair_time
	repair_timer.timeout.connect(repair)
	
	up_level_sprite.visible = false
	hit_area.monitorable = false
	hit_area.monitoring = false

func setup_segment(grid_pos: Vector2i):
	self.grid_pos = grid_pos
	if not grid_manager: grid_manager = get_node("/root/Main/GridManager")
	if grid_manager: grid_manager.set_grid_occupied(grid_pos, self)

func setup_bridge_tile(neighbors: Dictionary):
	var has_north = neighbors.get("north", false)
	var has_south = neighbors.get("south", false)
	var has_east = neighbors.get("east", false)
	var has_west = neighbors.get("west", false)
	var connection_count = [has_north, has_south, has_east, has_west].count(true)
	
	match connection_count:
		4:
			tile_animation_name = "四向"
		3:
			tile_animation_name = "三向"
			if not has_west: animated_sprite.rotation_degrees = 0
			elif not has_north: animated_sprite.rotation_degrees = 90
			elif not has_east: animated_sprite.rotation_degrees = 180
			elif not has_south: animated_sprite.rotation_degrees = 270
		2:
			if (has_north and has_south):
				tile_animation_name = "二向"
				animated_sprite.rotation_degrees = 0
			elif (has_east and has_west):
				tile_animation_name = "二向"
				animated_sprite.rotation_degrees = 90
			else:
				tile_animation_name = "拐角"
				if has_north and has_east: animated_sprite.rotation_degrees = 0
				elif has_south and has_east: animated_sprite.rotation_degrees = 90
				elif has_south and has_west: animated_sprite.rotation_degrees = 180
				elif has_north and has_west: animated_sprite.rotation_degrees = 270
		1:
			tile_animation_name = "单向"
			if has_south: animated_sprite.rotation_degrees = 0
			elif has_west: animated_sprite.rotation_degrees = 90
			elif has_north: animated_sprite.rotation_degrees = 180
			elif has_east: animated_sprite.rotation_degrees = 270
		_:
			tile_animation_name = "单向"
	
	animated_sprite.animation = tile_animation_name

func take_damage(amount: float):
	if is_destroyed: return
	current_health -= amount
	if current_health <= 0:
		current_health = 0
		is_destroyed = true
		animated_sprite.modulate = Color(0.4, 0.4, 0.4)
		animated_sprite.stop()
		if is_attack_upgraded:
			up_level_sprite.visible = false
			hit_area.monitorable = false
			hit_area.monitoring = false
		print("桥段 %s 已被摧毁！" % grid_pos)

func repair():
	is_destroyed = false
	current_health = max_health
	animated_sprite.modulate = Color.WHITE
	animated_sprite.animation = tile_animation_name
	animated_sprite.frame = animated_sprite.sprite_frames.get_frame_count(tile_animation_name) - 1
	if is_attack_upgraded:
		apply_attack_upgrade()

func apply_attack_upgrade():
	if is_attack_upgraded: return
	is_attack_upgraded = true
	up_level_sprite.visible = true
	up_level_sprite.frame = 0 # 设置为攻击升级图案的第一帧
	hit_area.monitorable = true
	hit_area.monitoring = true
	print("桥段 %s 应用了攻击升级！" % grid_pos)

func _on_area_2d_mouse_entered() -> void:
	if not is_destroyed and repair_timer.is_stopped():
		animated_sprite.modulate = Color(0.8, 0.8, 0.5)

func _on_area_2d_mouse_exited() -> void:
	if not is_destroyed and repair_timer.is_stopped():
		animated_sprite.modulate = Color.WHITE

func _on_area_2d_input_event(viewport: Node, event: InputEvent, shape_idx: int) -> void:
	if event is InputEventMouseButton and event.button_index == MOUSE_BUTTON_LEFT and event.is_pressed():
		if is_destroyed and repair_timer.is_stopped():
			animated_sprite.modulate = Color(0.2, 0.5, 1.0)
			animated_sprite.animation = tile_animation_name
			animated_sprite.play()
			repair_timer.start()
		elif not is_destroyed:
			emit_signal("bridge_selected", self)
		get_viewport().set_input_as_handled()
